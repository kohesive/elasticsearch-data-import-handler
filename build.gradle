plugins {
    id 'nebula.kotlin' version '1.1.61' apply false
    id 'nebula.resolution-rules' version '5.1.1' apply false
}

apply plugin: 'nebula.kotlin'
apply plugin: 'nebula.resolution-rules'
apply plugin: 'java'
apply plugin: 'idea'

apply plugin: 'application'

mainClassName = 'uy.kohesive.elasticsearch.dataimport.App'
applicationName = 'kohesive-es-dih'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

dependencies {
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jre8', version: version_kotlin
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: version_kotlin

    compile group: 'com.fasterxml.jackson.module', name: 'jackson-module-kotlin', version: version_jackson_kotlin_module
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: version_jackson_compatible_with_spark
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: version_jackson_compatible_with_spark

    compile group: 'com.typesafe', name: 'config', version: version_typesafe_config

    compile group: 'org.elasticsearch', name: "elasticsearch-spark-20_${version_scala}", version: version_elasticsearch
    compile group: 'com.squareup.okhttp3', name: 'okhttp', version: version_okhttp

    compile group: 'org.apache.spark', name: "spark-sql_${version_scala}", version: version_spark

    compile group: 'org.jsoup', name: 'jsoup', version: version_jsoup

    compile (group: 'com.algolia', name: 'algoliasearch', version: version_algolia) {
        exclude group: 'com.fasterxml.jackson.module'
        exclude group: 'com.fasterxml.jackson.datatype'
        exclude group: 'com.fasterxml.jackson.core'
    }

    resolutionRules group: 'com.netflix.nebula', name: 'gradle-resolution-rules', version: version_nebula_resolution
    resolutionRules files("${rootDir}/gradle-local-dependency-rules.json")

    compile group: 'org.slf4j', name: 'slf4j-api', version: version_slf4j
    runtime group: 'ch.qos.logback', name: 'logback-classic', version: version_logback

    compile group: 'mysql', name: 'mysql-connector-java', version: version_jdbc_mysql
    compile group: 'org.postgresql', name: 'postgresql', version: version_jdbc_postresql

    testCompile group: 'junit', name: 'junit', version: version_junit
    testCompile  group: 'org.jetbrains.kotlin', name: 'kotlin-test-junit'

}

nebulaResolutionRules {
    optional = ['slf4j-bridge']
}

repositories {
    mavenCentral()
}

compileKotlin {
    kotlinOptions {
        jvmTarget = 1.8
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = version_gradle
}

tasks.withType(Tar){
    compression = Compression.GZIP
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}